; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppName "Buzz"
#define AppDisplayName "Buzz"
#define AppExeName "Buzz.exe"
#define AppPublisher "Dinova/DTIP/PCRS"
#define AppIconPath "assets\buzz_pcrs.ico"
#define AppSourcePath "dist\Buzz\*"
#define OutputDir "dist"
#define AppRegKey "Software\Buzz"
#define AppVersion "1.0.0"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{574290A2-EF7C-4845-85F3-BFF2F011A580}
AppName={#AppName}
AppVersion={#AppVersion}
;AppVerName={#AppName} {#AppVersion}
DefaultDirName={autopf}\{#AppName}
DisableProgramGroupPage=yes
; (Un)comment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=lowest
; Uncomment the following line to allow admin-level installation
; PrivilegesRequiredOverridesAllowed=dialog
OutputDir={#OutputDir}
OutputBaseFilename={#AppDisplayName}-{#AppVersion}-windows
SetupIconFile={#AppIconPath}
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "portuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
; below is the original line
;Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "installffmpeg"; Description: "Configurar FFmpeg (necessário para transcrição de áudio)"; GroupDescription: "Componentes adicionais:"; Flags: checkedonce

[Files]
Source: {#AppSourcePath}; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; Adicionado para garantir que o FFmpeg esteja incluído corretamente
Source: "ffmpeg\*"; DestDir: "{app}\ffmpeg"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: installffmpeg
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#AppName}"; Filename: "{app}\{#AppExeName}"
Name: "{autodesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(AppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKCU; Subkey: "{#AppRegKey}"; ValueType: string; ValueName: "ui-locale"; ValueData: "pt_BR"
; Adicionar uma entrada para o ffmpeg
Root: HKCU; Subkey: "{#AppRegKey}\FFmpeg"; ValueType: string; ValueName: "Path"; ValueData: "{app}\ffmpeg"

[Code]
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usPostUninstall then
  begin
    if RegKeyExists(HKEY_CURRENT_USER, '{#AppRegKey}') then
      if MsgBox('Deseja excluir as configurações do Buzz?', mbConfirmation, MB_YESNO) = IDYES
      then
        RegDeleteKeyIncludingSubkeys(HKEY_CURRENT_USER, '{#AppRegKey}');
  end;
end;

procedure DeleteFileOrFolder(FilePath: string);
begin
  if FileExists(FilePath) then
  begin
    DeleteFile(FilePath);
  end
  else if DirExists(FilePath) then
  begin
    DelTree(FilePath, True, True, True);
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssInstall then
  begin
    DeleteFileOrFolder(ExpandConstant('{app}\Buzz.exe'));
    DeleteFileOrFolder(ExpandConstant('{app}\_internal'));
  end;
  
  if CurStep = ssPostInstall then
  begin
    // Registrar informações adicionais
    RegWriteStringValue(HKEY_CURRENT_USER, '{#AppRegKey}', 'InstallDate', GetDateTimeString('yyyy-mm-dd hh:nn:ss', '-', ':'));
    RegWriteStringValue(HKEY_CURRENT_USER, '{#AppRegKey}', 'FFmpegInstalled', 'True');
  end;
end;